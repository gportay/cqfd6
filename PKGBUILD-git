# Maintainer: GaÃ«l PORTAY <gael.portay@gmail.com>

pkgname=cqfd6-git
pkgver=master
pkgrel=1
pkgdesc='Wrap commands in controlled Docker containers using docker.'
arch=(any)
url="https://github.com/gportay/$pkgname"
license=(GPL-3.0-or-later)
depends=(bash)
makedepends=(asciidoctor bash-completion git)
checkdepends=(shellcheck)
source=("$pkgname::git+https://github.com/gportay/${pkgname%-git}.git")
sha256sums=(SKIP)
install="$pkgname.install"

pkgver() {
	cd "$srcdir/$pkgname"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "$srcdir/$pkgname"
	make doc
}

check() {
	cd "$srcdir/$pkgname"
	make -k check
}

package() {
	depends+=(docker)
	provides+=("${pkgname%-git}")
	conflicts+=("${pkgname%-git}")

	cd "$srcdir/$pkgname"
	make DESTDIR="$pkgdir" PREFIX="/usr" install
	install -D -m 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
